
plugins {
  id 'com.github.johnrengelman.shadow' version '4.0.3'
  id 'java'
  id 'application'
  id 'edu.sc.seis.launch4j' version '2.4.6'
  id 'org.gradle.crypto.checksum' version '1.1.0'
  id 'com.palantir.git-version' version '0.12.2'
  id "edu.sc.seis.macAppBundle" version "2.3.0"
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.burst-apps-team:burstkit4j:734c9f83bf'
    //implementation 'com.github.jjos2372:burstkit4j:f1364a8ded'
    //implementation 'com.github.jjos2372:blocktalk:-SNAPSHOT'
    implementation 'com.github.jjos2372:blocktalk:415d8a8f78'

    implementation 'com.github.jiconfont:jiconfont-swing:1.0.0'
    implementation 'com.github.jiconfont:jiconfont-font_awesome:4.7.0.1'
    
	implementation 'com.bulenkov:darcula:2018.2'
	
	implementation 'io.github.novacrypto:BIP39:2019.01.27'
	
	implementation 'jfree:jfreechart:1.0.13'
	
	implementation 'io.github.monero-ecosystem:monero-java:0.2.1'
	
	implementation 'fr.marcwrobel:jbanking:1.0'
	
    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

// If we want to refresh our libraries (get a new snapshot)
configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// Define the main class for the application
mainClassName = 'BTDEX'

def details = versionDetails()

task versionProp()  {
    doFirst {
        new File(sourceSets.main.output.resourcesDir, "/version.properties").text = "version:${details.lastTag}"
    }
}
shadowJar.dependsOn versionProp

createExe {
    outfile = 'btdex.exe'
    icon = "${projectDir}/btdex.ico"
    jreMinVersion = '1.8.0'
    copyright = 'BTDEX'
    companyName = 'https://btdex.trade'
    textVersion = details.lastTag
    
    mainClassName = project.mainClassName
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
}

createDmg.enabled = false
macAppBundle {
  mainClassName = project.mainClassName
  icon = "${projectDir}/btdex.icns"
  appStyle = 'Oracle'
  appName = project.applicationName
  dmgName = "${->project.name}-mac"
  runtimeConfigurationName = 'shadow'
  jarTask = 'shadowJar'
  jvmVersion = "1.8+"
}

import org.gradle.crypto.checksum.Checksum

task release(type: Checksum, dependsOn: [shadowJar, createExe, createAppZip]) {
  files = project.tasks.shadowJar.outputs.files.plus(files(['build/launch4j/btdex.exe', 'build/distributions/btdex-mac.zip']))
}

