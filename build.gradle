plugins {
	id 'com.github.johnrengelman.shadow' version '7.0.0' // Updated from 4.0.4
	id 'java'
	id 'application'
	id 'edu.sc.seis.launch4j' version '2.5.3' // Updated from 2.4.6
	id 'edu.sc.seis.macAppBundle' version '2.3.0'
	id "nebula.ospackage" version "9.0.0" // Updated from 8.2.0
	id 'org.gradle.crypto.checksum' version '1.4.0' // Updated from 1.2.0
	id 'com.palantir.git-version' version '0.15.0' // Updated from 0.12.2
	id 'io.spring.dependency-management' version '1.0.11.RELEASE' // Updated from 1.0.1.RELEASE
	id "de.undercouch.download" version "4.1.2" // Updated from 1.2
}


java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.signum-network:signumj:abd8563d73'
    implementation 'com.github.signum-network:signum-smartj:89e2f28bb3'

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.15'
    implementation 'com.squareup.okhttp3:okhttp:4.4.0'

    implementation 'com.github.jiconfont:jiconfont-swing:1.0.0'
    //implementation 'com.github.jiconfont:jiconfont-font_awesome:4.7.0.1'
    implementation 'com.github.meisenla:jiconfont-font_awesome:8be36e8ba9'

    implementation 'com.github.jjos2372:java-sdk:4739af9c11'

    implementation 'com.github.sealedtx:bitcoin-cash-converter:1.0'

    //implementation 'net.java.dev.jna:jna-platform:5.7.0'
    //implementation 'net.java.dev.jna:jna:5.7.0'

	//implementation 'com.github.weisj:darklaf-core:2.5.5'
    //implementation 'com.bulenkov:darcula:2018.2'
    implementation 'com.github.jjos2372:Darcula:3b47831130' // this one is compatible with Java16
    //implementation 'com.formdev:flatlaf:1.5'
    //implementation 'com.formdev:flatlaf-intellij-themes:1.5'

    implementation 'com.dorkbox:SystemTray:3.17'

    implementation 'org.apache.commons:commons-text:1.8'

    implementation 'io.github.novacrypto:BIP39:2019.01.27'

    implementation 'jfree:jfreechart:1.0.13'

    implementation 'com.github.kenglxn.QRGen:javase:2.6.0'

    implementation 'io.github.monero-ecosystem:monero-java:0.2.1'

    implementation 'fr.marcwrobel:jbanking:1.0'

    implementation 'com.github.jjos2372:ledger4j:06fb465b5a'

    implementation 'org.nanohttpd:nanohttpd:2.3.1'

    implementation 'com.github.stellar:java-stellar-sdk:0.32.0'

	//logging
	implementation 'org.apache.logging.log4j:log4j-api:2.15.0'
	implementation 'org.apache.logging.log4j:log4j-core:2.15.0'

    // Use JUnit test framework
	testImplementation("org.junit.platform:junit-platform-engine:1.8.0")
	testImplementation("org.junit.platform:junit-platform-launcher:1.8.0")
	testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.0")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.0")
//    testImplementation("org.junit.platform:junit-platform-engine:1.6.1")
//    testImplementation("org.junit.platform:junit-platform-launcher:1.6.1")
//    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.1")
//    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.1")
}
test {
    useJUnitPlatform()
}
// If we want to refresh our libraries (get a new snapshot)
configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// Define the main class for the application
application {
	mainClass.set('BTDEX')
}
description = 'A decentralized exchange system running on the Signum blockchain.'

def details = versionDetails()

task versionProp()  {
    doFirst {
        new File(sourceSets.main.output.resourcesDir, "/version.properties").text = "version:${details.lastTag}"
    }
}

jar {
	// Needed by the loggin library
    manifest {
        attributes 'Multi-Release': 'true'
    }
}

shadowJar {
	archiveFileName = "${baseName}-${classifier}.${extension}"  // archiveName is deprecated
}
shadowJar.dependsOn versionProp

def jdkVersion = 'zulu11.56.19-ca-jdk11.0.15'  // Updated from zulu8.54.0.21
import de.undercouch.gradle.tasks.download.Download

task downloadWindowsJDK(type: Download) {
    src "https://cdn.azul.com/zulu/bin/${jdkVersion}-win_x64.zip"
    dest "dist/jdk/${jdkVersion}-win_x64.zip"
}
task unzipWindowsJDK(type: Copy, dependsOn: downloadWindowsJDK) {
    from(zipTree("dist/jdk/${jdkVersion}-win_x64.zip"))
    into "dist/jdk/"
}

createExe {
    outfile = 'btdex.exe'
    icon = "${projectDir}/dist/btdex.ico"
    jreMinVersion = '11.0.0'
    copyright = 'BTDEX'
    companyName = 'https://btdex.trade'
    textVersion = details.lastTag

    bundledJrePath = "./jre"
    bundledJre64Bit = false

    mainClassName = project.mainClassName
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
}

task windowsZip(type: Zip) {
   dependsOn createExe
   dependsOn unzipWindowsJDK
   from "${project.buildDir}/launch4j/${baseName}.exe"
   into('jre'){
   	from "dist/jdk/${jdkVersion}-win_x64/jre"
   }
   archiveName "${baseName}-win_x64.zip"
}

task createInstaller(type:Exec) {
   dependsOn createExe
   dependsOn unzipWindowsJDK
   workingDir "${project.buildDir}/distributions"

   commandLine 'makensis', "${projectDir}/dist/installer.nsi"
}

task downloadMacJDK(type: Download) {
    src "https://cdn.azul.com/zulu/bin/${jdkVersion}-macosx_x64.zip"
    dest "dist/jdk/${jdkVersion}-macosx_x64.zip"
}
task unzipMacJDK(type: Copy, dependsOn: downloadMacJDK) {
    from(zipTree("dist/jdk/${jdkVersion}-macosx_x64.zip"))
    into "dist/jdk/"
}
createDmg.enabled = false
macAppBundle {
	mainClassName = project.mainClassName
    version = details.lastTag
    icon = "${projectDir}/dist/btdex.icns"
    appStyle = 'Oracle'
    appName = project.applicationName
    dmgName = "${->project.name}-macosx_x64"
    runtimeConfigurationName = 'shadow'
    jarTask = 'shadowJar'
    jvmVersion = "11+"
    bundleJRE = true
    jreHome = "${projectDir}/dist/jdk/${jdkVersion}-macosx_x64/zulu-8.jdk/Contents/Home"
}
createAppZip.dependsOn unzipMacJDK


task downloadLinuxJDK(type: Download) {
    src "https://cdn.azul.com/zulu/bin/${jdkVersion}-linux_x64.zip"
    dest "dist/jdk/${jdkVersion}-linux_x64.zip"
}
task unzipLinuxJDK(type: Copy) {
    from(zipTree("dist/jdk/${jdkVersion}-linux_x64.zip"))
    into "dist/jdk/"
}
unzipLinuxJDK.dependsOn downloadLinuxJDK
task makeDeb(type: Deb) {
	dependsOn shadowJar
	dependsOn unzipLinuxJDK

    maintainer 'btdex.trade'
    license	'GPL-3+'
    version details.lastTag.substring(1)
    release '1'
    arch 'amd64'

    requires('xdg-utils')

    from('dist/copyright'){
        into '/usr/share/doc/btdex'
    }

    from("dist/jdk/${jdkVersion}-linux_x64/jre"){
        into '/opt/btdex/jre'
    }

    into '/opt/btdex'
    from('dist/btdex.sh'){
        fileMode 0755
    }
    from(new File(sourceSets.main.output.resourcesDir, "/icon.png"))
    link('/usr/share/pixmaps/btdex.png', '/opt/btdex/icon.png')
    link('/usr/bin/btdex', '/opt/btdex/btdex.sh')
    from('dist/btdex.desktop'){
        into '/usr/share/applications'
    }
    from(project.tasks.shadowJar.outputs.files)
}

import org.gradle.crypto.checksum.Checksum

task makeDebChecksum(type: Checksum, dependsOn: [makeDeb]) {
}
task release(type: Checksum, dependsOn: [shadowJar, windowsZip, createAppZip, makeDeb,
	//createInstaller
	]) {
    files = project.tasks.shadowJar.outputs.files.plus(files(['build/distributions/btdex-win_x64.zip',
    	'build/launch4j/btdex.exe',
    	'build/distributions/btdex-macosx_x64.zip',
    	// 'dist/btdex-installer-win_x64.exe'
    	])).plus(project.tasks.makeDeb.outputs.files)
}
